TITLE:: GFX_Overview
SUMMARY:: GFX system overview
CATEGORIES:: Green
RELATED:: Classes/GFX_Module, Classes/GFX_Rack

DESCRIPTION::

Green effects module system with optional GUI.

Based on redModule in the redSys quark ( https://github.com/redFrik/redSys )

Provides SynthDef+Synth construction, pseudo UGen support, code<->GUI linkage and automatic GUI generation with minimal code (see Modules folder - basically just an CODE::*ar:: method and some specs - see LINK::Classes/GFX_Module#Writing custom modules::).


SUBSECTION:: Class hierarchy

Normally one would select a few modules and put them in a rack...
CODE::
GFX_Rack([GFXFreq, GFXPch2, GFXTank]);
::
and not use the LINK::Classes/AbstractGFX:: and LINK::Classes/GFX_Module:: classes.


STRONG::Available GFX classes:::

CLASSTREE:: AbstractGFX

STRONG:: Supporting GUI classes:::

CLASSTREE:: AbstractGFXGUI

and for these GUI classes just call CODE::.gui:: on the rack or on some effect module directly.


SUBSECTION:: Optional GUI

There are two versions. The standard one with knobs (v0) and a more compact one (v1) with silders and numberboxes.

IMAGE::../Images/GFX_RackGUI_v0.png::
IMAGE::../Images/GFX_RackGUI_v1.png::
IMAGE::../Images/GFXPch2GUI_v0.png::
IMAGE::../Images/GFXPch2GUI_v1.png::
IMAGE::../Images/GFXTankGUI_v0.png::
IMAGE::../Images/GFXTankGUI_v1.png::

The LINK::Classes/AbstractGFX#-gui::. method has an argument for which version to use.

EXAMPLES::

SUBSECTION:: module

CODE::
s.boot;

g= GFXPch2();
a= {SinOsc.ar([400, 404], 0, 0.1)}.play;
g.pch2Mix= 0.5;
g.pch2Ratio= 1.1;
g.pch2Ratio;

g.values;
g.cvs;
g.specs;
g.numChannels;

//--optional GUI
g.gui;
g.gui(version:1);

g.restoreDefault;
g.free;
a.free;
::

SUBSECTION:: as UGen

CODE::
a= {GFXPch2.ar(SinOsc.ar([400, 404], 0, 0.1), MouseX.kr(-4, 4))}.play;
a.free;

//modules automatically adapt to any number of channels
a= {GFXPch2.ar(SinOsc.ar(400, 0, 0.1), MouseX.kr(-4, 4))}.play;
a.free;
::

SUBSECTION:: rack

CODE::
a= {SinOsc.ar([400, 404], 0, 0.1)}.play;
r= GFX_Rack([GFXFreq, GFXTanh, GFXPch2, GFXTank]);
r.pch2Mix= 0.5;
r.tanhMix= 0.25;
r.freqMix= 1;
r.freqShift= 10;

//--optional gui
r.gui;
r.gui(version:1);

r.free;
a.free;
::